10 SCREEN $80
20 COLOR 1
30 C=0:XC=160:YC=120:R=90:R1=100:R2=110
40 BANK 1
50 RECT 0,0,319,239,0
60 REM CLOCK
70 AI=2*\XFF/60
80 A1=0:A2=\XFF*2
90 C=1
100 FOR I=A1+AI*5 TO A2  STEP AI*5
110 XR=SIN(I)*R+XC
120 XT=SIN(I)*R1+XC
130 XU=SIN(I)*R2+XC
140 YR=-COS(I)*R+YC
150 YT=-COS(I)*R1+YC
160 YU=-COS(I)*R2+YC
170 LINE XR,YR,XT,YT,3
180 CHAR XU-6,YU+3,1,STR$(C)
190 C=C+1
200 NEXT
210 REM CLOCK
220 T$=TI$
230 HH=VAL(MID$(T$,1,2))
240 MM=VAL(MID$(T$,3,2))
250 SS=VAL(MID$(T$,5,2))
260 TT$=LEFT$(T$,2)+":"+MID$(T$,3,2)+":"+RIGHT$(T$,2)
270 LINE XC,YC,XS,YS,0
280 LINE XC,YC,XM,YM,0
290 LINE XC,YC,XH,YH,0
300 AA=AI*SS
310 XS=SIN(AA)*R+XC
320 YS=-COS(AA)*R+YC
330 LINE XC,YC,XS,YS,14
340 AA=AI*MM
350 XM=SIN(AA)*R*.8+XC
360 YM=-COS(AA)*R*.8+YC
370 LINE XC,YC,XM,YM,4
380 AA=AI*HH*2.5
390 XH=SIN(AA)*R*.6+XC
400 YH=-COS(AA)*R*.6+YC
410 LINE XC,YC,XH,YH,5
420 RECT XC-18,YC+5,XC+18,YC-5,0
430 CHAR XC-17,YC+3,1,TT$
440 IF T$=TI$ THEN 440
450 GOTO 220
