10 REM ANIMATES N RANDOMLY COLORED BALLS BOUNCING AROUND THE SCREEN
20 REM THE SCREEN IS 40 BY 30 AND THE BALL IS TEXT CHARACTER 81
30 SCREEN 3                        : REM SWITCH SCREEN TO 40 CHARACTER MODE
40 PRINT "ENTER HOW MANY BALLS TO DRAW (1-255)?"
50 INPUT N
60 IF N<1 OR N>255 THEN 40         : REM IF INPUT IS OUT OF RANGE, ASK AGAIN
70 PRINT CHR$(147);                : REM CLEAR SCREEN, DO NOT MOVE CURSOR
80 REM INITIALIZE BALL INFO ARRAYS
90 DIM BX(N): DIM BY(N): DIM BC(N) : REM BALL POSITION AND COLOR
100 DIM DX(N): DIM DY(N)           : REM BALL DIRECTION
110 FOR B=0 TO N
120 BX(B)=INT(RND(1)*40)*2+1       : REM RANDOM COLUMN (X VALUE)
130 BY(B)=INT(RND(1)*30)*$100      : REM RANDOM ROW (Y VALUE)
140 BC(B)=INT(RND(1)*14)+1         : REM RANDOM BALL COLOR
150 DX(B)=INT(RND(1)*2)            : REM RANDOM X DIRECTION
160 DY(B)=1                        : REM MOVE DOWN
170 NEXT B
180 REM ANIMATE BLANKING OF SCREEN
190 REM IN TEXT MODE 1ST BYTE IS CHARACTER, NEXT BYTE IS COLOR
200 FOR Y=0 TO 29 : FOR X=0 TO 39
210 Q=$B000+(Y*$100)+(X*2)         : REM CALC POSITION ON SCREEN
220 VPOKE 1,Q,81                   : REM DRAW CIRCLE
230 VPOKE 1,Q+1,0                  : REM SET COLOR BLACK
240 NEXT X,Y
250 REM MAIN LOOP - ANIMATE BALLS BOUNCING
260 FOR B=1 TO N
270 VPOKE 1,$B000+BY(B)+BX(B),0     : REM DRAW BLANK AT CURRENT BALL LOCATION
280 IF DX(B)=0 THEN GOSUB 360       : REM GOSUB IF BALL DIR LEFT
290 IF DX(B)=1 THEN GOSUB 390       : REM GOSUB IF BALL DIR RIGHT
300 IF DY(B)=0 THEN GOSUB 430       : REM GOSUB IF BALL DIR DOWN
310 IF DY(B)=1 THEN GOSUB 460       : REM GOSUB IF BALL DIR UP
320 VPOKE 1,$B000+BY(B)+BX(B),BC(B) : REM DRAW BALL AT NEW POSITION
330 NEXT B
340 GOTO 260                        : REM LOOP FOREVER ANIMATING THE BALLS
350 REM X DIRECTION IS 0, MOVE BALL LEFT
360 IF BX(B)=1 THEN BX(B) = 3 : DX(B)=1 : RETURN
370 BX(B)=BX(B)-2 : RETURN
380 REM X DIRECTION IS 1, MOVE BALL RIGHT
390 BX(B)=BX(B)
400 IF BX(B)=79 THEN BX(B)=77 : DX(B)=0 : RETURN
410 BX(B)=BX(B)+2 : RETURN
420 REM Y DIRECTION IS 0, MOVE BALL UP 1 ROW
430 IF BY(B)=0 THEN BY(B) = 256 : DY(B) = 1 : RETURN
440 BY(B)=BY(B)-$100 : RETURN
450 REM DY(B) IS 1, MOVE BALL DOWN A ROW
460 BY(B)=BY(B)
470 IF BY(B)=7424 THEN BY(B)=7168 : DY(B)=0 : RETURN
480 BY(B)=BY(B)+$100 : RETURN
